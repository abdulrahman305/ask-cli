namespace com.alexaprize

import com.amazon.alexa.ask.conversations.Affirm
import com.amazon.alexa.ask.conversations.Deny
import com.amazon.alexa.ask.conversations.Invoke
import com.amazon.alexa.ask.conversations.Notify
import com.amazon.alexa.ask.conversations.Offer
import com.amazon.alexa.ask.conversations.expect
import com.amazon.alexa.ask.conversations.response
import com.amazon.alexa.ask.conversations.utterances
import com.amazon.alexa.schema.Boolean
import com.amazon.alexa.schema.Nothing
import com.amazon.alexa.schema.String
import com.amazon.ask.types.builtins.AMAZON.NUMBER
import prompts.alexa_prize_preconditions_apla
import slotTypes.TopicType

type ConversationDetails {
  optional TopicType Topic
}

letsChatEvent = utterances<ConversationDetails>(
  samples = [
    "let's chat",
    "i want to chat",
    "chat",
    "talk",
    "let's chat about {Topic}",
    "i want to talk about {Topic}",
    "can we talk about {Topic}"
  ]
)

affirmEvent = utterances(
  samples = [
    "yes",
    "yup",
    "yeah",
    "sure",
    "alright"
  ]
)

denyEvent = utterances(
  samples = [
    "no",
    "nope",
    "not really",
    "not",
    "naah"
  ]
)

type AlexaPrizePreconditions {
  NUMBER isLocaleSupported
  NUMBER isDeviceSupported
  NUMBER isFalseWake
  NUMBER isSocialBotAvailable
  NUMBER isAlexaPrizeEnabled
  NUMBER isTopicBanned
  String handoffPromptOverride
}

type AlexaPrizePreconditionsPayload {
  AlexaPrizePreconditions result
}

type HandoffResults {
  String handoffPromptOverride
}

type HandoffResultsPayload {
  HandoffResults result
}

action AlexaPrizePreconditions checkPreconditionsAndHandoff(TopicType Topic = nothing)

action HandoffResults intentConfirmedHandoff()

action HandoffResults intentDeniedHandoff()

dialog Nothing AlexaPrizeDialog() {
  sample {
    preconditionsResult = checkPrecondition()
    isHandoffPossible = (((preconditionsResult.isDeviceSupported == 1 && preconditionsResult.isLocaleSupported == 1) && preconditionsResult.isSocialBotAvailable == 1) && preconditionsResult.isAlexaPrizeEnabled == 1)
    if (isHandoffPossible) {
      isConfirmationNeeded = (preconditionsResult.isFalseWake == 1 || preconditionsResult.isTopicBanned == 1)
      responses(
        isConfirmationNeeded = isConfirmationNeeded,
        preconditionsResult = preconditionsResult
      )
    } else {
      response(
        response = alexa_prize_preconditions_apla,
        act = Notify {
          actionName = checkPreconditionsAndHandoff,
          success = false
        },
        payload = AlexaPrizePreconditionsPayload {
          result = preconditionsResult
        }
      )
    }
  }
}

dialog AlexaPrizePreconditions checkPrecondition() {
  sample {
    chatRequest = expect(
      act = Invoke,
      event = letsChatEvent
    )
    checkPreconditionsAndHandoff(
      Topic = chatRequest.Topic
    )
  }
  sample {
    expect(
      act = Invoke,
      event = letsChatEvent
    )
    preconditionsResult = checkPreconditionsAndHandoff()
  }
}

dialog Nothing responses(Boolean isConfirmationNeeded, AlexaPrizePreconditions preconditionsResult) {
  sample {
    if (isConfirmationNeeded) {
      handoffResult(
        preconditionsResult = preconditionsResult
      )
    } else {
      response(
        response = alexa_prize_preconditions_apla,
        act = Notify {
          actionName = checkPreconditionsAndHandoff
        },
        payload = AlexaPrizePreconditionsPayload {
          result = preconditionsResult
        }
      )
    }
  }
}

dialog Nothing handoffResult(AlexaPrizePreconditions preconditionsResult) {
  sample {
    response(
      response = alexa_prize_preconditions_apla,
      act = Notify {
        actionName = checkPreconditionsAndHandoff
      },
      nextAct = Offer {
        actionName = intentConfirmedHandoff
      },
      payload = AlexaPrizePreconditionsPayload {
        result = preconditionsResult
      }
    )
    expect(
      act = Affirm,
      event = affirmEvent
    )
    handoffResults = intentConfirmedHandoff()
    response(
      response = alexa_prize_preconditions_apla,
      act = Notify {
        actionName = intentConfirmedHandoff
      },
      payload = HandoffResultsPayload {
        result = handoffResults
      }
    )
  }
  sample {
    response(
      response = alexa_prize_preconditions_apla,
      act = Notify {
        actionName = checkPreconditionsAndHandoff
      },
      nextAct = Offer {
        actionName = intentDeniedHandoff
      },
      payload = AlexaPrizePreconditionsPayload {
        result = preconditionsResult
      }
    )
    expect(
      act = Deny,
      event = denyEvent
    )
    handoffResults = intentDeniedHandoff()
    response(
      response = alexa_prize_preconditions_apla,
      act = Notify {
        actionName = intentDeniedHandoff
      },
      payload = HandoffResultsPayload {
        result = handoffResults
      }
    )
  }
}
