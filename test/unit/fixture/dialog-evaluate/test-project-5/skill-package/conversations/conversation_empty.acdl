namespace com.numberbot

import com.amazon.alexa.ask.conversations.APLA
import com.amazon.alexa.ask.conversations.Action
import com.amazon.alexa.ask.conversations.Bye
import com.amazon.alexa.ask.conversations.Invoke
import com.amazon.alexa.ask.conversations.Notify
import com.amazon.alexa.ask.conversations.Offer
import com.amazon.alexa.ask.conversations.RequestArguments
import com.amazon.alexa.ask.conversations.ensure
import com.amazon.alexa.ask.conversations.expect
import com.amazon.alexa.ask.conversations.response
import com.amazon.alexa.ask.conversations.utterances
import com.amazon.alexa.schema.Nothing
import com.amazon.alexa.schema.Number
import com.amazon.alexa.schema.Thing
import com.amazon.ask.types.builtins.AMAZON.NUMBER
import prompts.AlexaConversationsBye
import prompts.numericfeedback_apla
import prompts.rating_apla

type NumPayload {
  Number num
}

type RatingRequest {
  NUMBER rating
}

InformRatingEvent = utterances<RatingRequest>(
  [
    "my score is {rating}",
    "that was a {rating} conversation",
    "i rate you a {rating}",
    "{rating}"
  ]
)

getStart = utterances(
  [
    "give me a number please"
  ]
)

action Nothing saveRating(Number rating)

action Number getNumber()

dialog Nothing GetFeedback(APLA<Thing> feedback_prompt, Thing payload, Action notifyAction) {
  sample {
    response(
      feedback_prompt,
      Notify {
        actionName = notifyAction,
        success = true
      },
      Offer {
        actionName = saveRating,
        arguments = [
          saveRating.arguments.rating
        ]
      },
      payload,
      nothing
    )
    myRating = expect(
      Invoke,
      InformRatingEvent
    )
    ensure(
      RequestArguments<Thing> {
          arguments = [
            saveRating.arguments.rating
          ],
          response = rating_apla
        }
    )
    saveRating(
      myRating.rating
    )
  }
}

dialog Nothing MainDialog() {
  sample {
    expect(
      Invoke,
      getStart
    )
    num = getNumber()
    GetFeedback(
      feedback_prompt = numericfeedback_apla,
      payload = NumPayload {
        num = num
      },
      notifyAction = getNumber
    )
    response(
      AlexaConversationsBye,
      Notify {
        actionName = saveRating,
        success = true
      },
      Bye {},
      nothing,
      nothing
    )
  }
}
